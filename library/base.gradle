apply from: (project(':library').projectDir.toString() + '/baseForAll.gradle')
android {
    defaultConfig {
        versionCode app_version_code.toInteger()
        versionName app_version
    }

    flavorDimensions "mode", "stage"

    productFlavors {
        free {
            dimension "mode"
            applicationId application_id
        }

        pro {
            dimension "mode"
            applicationId application_id + ".pro"
        }

        dev {
            dimension "stage"
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
        }

        prod {
            dimension "stage"
        }

        mock {
            dimension "stage"
            applicationIdSuffix ".mock"
            versionNameSuffix "-mock"
        }
    }

    signingConfigs {
        release {
            keyAlias androidKeyAlias
            keyPassword androidKeyPassword
            storeFile file(androidStoreFile)
            storePassword androidStorePassword
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }

        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', project(':library').projectDir.toString() + '/proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    //todo will this be working?
    implementation fileTree(include: ['*.jar', '*.aar'], dir: 'libs')
    implementation project(':library')
    testImplementation project(':testing')
    androidTestImplementation project(':androidtesting')
    //todo library, androidtesting, base.gradle can be split in dependency except for this kapt. we have to consider. whether split them or make one package
    kapt "androidx.room:room-compiler:$version_room"
}