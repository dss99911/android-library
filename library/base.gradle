apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'androidx.navigation.safeargs'

apply from: (project(':library').projectDir.toString() + '/androidTestDependencies.gradle')
apply from: (project(':library').projectDir.toString() + '/testDependencies.gradle')

android {
    compileSdkVersion compile_sdk_version.toInteger()
    buildToolsVersion build_tool_version
    defaultConfig {
        versionCode app_version_code.toInteger()
        versionName app_version
        minSdkVersion min_sdk_version.toInteger()
        targetSdkVersion target_sdk_version.toInteger()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true

        vectorDrawables.useSupportLibrary = true

        buildConfigField "String", "freePackageName", "\"$application_id\""
        resValue "string", "app_version", app_version

        buildConfigField "boolean", "isFree", "false"
        buildConfigField "boolean", "isPro", "false"
        buildConfigField "boolean", "isDev", "false"
        buildConfigField "boolean", "isProd", "false"
        buildConfigField "boolean", "isMock", "false"
    }

    flavorDimensions "mode", "stage"

    productFlavors {
        free {
            dimension "mode"
            applicationId application_id
            buildConfigField "boolean", "isFree", "true"
        }

        pro {
            dimension "mode"
            applicationId application_id + ".pro"
            buildConfigField "boolean", "isPro", "true"
        }

        dev {
            dimension "stage"
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"

            buildConfigField "boolean", "isDev", "true"
            //optimize build time
            resConfigs "en", "hdpi"
            minSdkVersion min_sdk_version.toInteger() > 21 ? min_sdk_version.toInteger() : 21
        }

        prod {
            dimension "stage"
            buildConfigField "boolean", "isProd", "true"
        }

        mock {
            dimension "stage"
            applicationIdSuffix ".mock"
            versionNameSuffix "-mock"

            buildConfigField "boolean", "isMock", "true"
            //optimize build time
            resConfigs "en", "hdpi"
            minSdkVersion min_sdk_version.toInteger() > 21 ? min_sdk_version.toInteger() : 21
        }
    }

    signingConfigs {
        release {
            keyAlias androidKeyAlias
            keyPassword androidKeyPassword
            storeFile file(androidStoreFile)
            storePassword androidStorePassword
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }

        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', project(':library').projectDir.toString() + '/proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    // Remove mockRelease as it's not needed.
    android.variantFilter { variant ->
        if (variant.buildType.name == 'release'
                && variant.getFlavors().get(0).name == 'mock') {
            variant.setIgnore(true)
        }
    }

    dataBinding {
        enabled = true
        enabledForTests = true
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    // Always show the result of every unit test, even if it passes.
    testOptions {
        unitTests {
            includeAndroidResources = true

            all {
                testLogging {
                    events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
                }
            }
        }
        animationsDisabled = true
    }

    sourceSets {
        String sharedTestDir = 'src/sharedTest/java'
        test {
            java.srcDir sharedTestDir
            java.srcDir project(':library').projectDir.toString() + '/src/test/java'
        }
        androidTest {
            java.srcDir sharedTestDir
            java.srcDir project(':library').projectDir.toString() + '/src/androidTest/java'
        }
    }
}

dependencies {
    //todo will this be working?
    implementation fileTree(include: ['*.jar', '*.aar'], dir: 'libs')
    implementation project(':library')
    //todo library, androidtesting, base.gradle can be split in dependency except for this kapt. we have to consider. whether split them or make one package
    kapt "androidx.room:room-compiler:$version_room"
}