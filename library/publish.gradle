
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version '1.0.8'
group 'kim.jeonghyeon'
def artifactName = 'android-library'
publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/androidlibrary-release.aar")
            groupId this.group
            artifactId artifactName
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayApiKey
    publications = ['Production']
    configurations = ['archives']
    override = true
    pkg {
        repo = 'maven'
        name = 'android-library'
        description = "android library"
        publish = true
        publicDownloadNumbers = true
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/dss99911/android-library.git'
        version {
            name = this.version
            desc = "Android library"
            released = new Date()
            vcsTag = this.version
        }
    }
}