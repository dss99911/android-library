apply plugin: 'maven-publish'

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

def pomConfig = {
    licenses {
        license {
            //noinspection GroovyAssignabilityCheck
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "hyun-kim"
            //noinspection GroovyAssignabilityCheck
            name "Hyun Kim"
            email "dss99911@gmail.com"
        }
    }

    scm {
        url "https://github.com/dss99911/kotlin-simple-architecture"
    }
}

publishing {
    repositories {
        maven {
            url  = "https://api.bintray.com/maven/hyun/kotlin-simple-architecture/${project.name}/;publish=1;override=1"
            credentials {
                username = bintrayUser
                password = bintrayApiKey
            }
        }
    }

    publications {
        Production(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId project.name
            version = project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', "Kotlin Simple Architecture - ${project.name}")
                root.appendNode('name', "Kotlin Simple Architecture - ${project.name}")
                root.appendNode('url', "https://github.com/dss99911/kotlin-simple-architecture")
                root.children().last() + pomConfig
            }
        }
    }
}
publish.dependsOn build